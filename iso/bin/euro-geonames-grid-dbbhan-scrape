#!/usr/bin/perl -I../perllib

use strict;
use warnings;
use LWP::Simple;
use JSON;
use DBI;

use DBBahn;

$|=1;

my $spacing = 1;
my $maxgeonames = 10;
my $url = 'http://ws.geonames.org/citiesJSON?north=%f&south=%f&east=%f&west=%f&maxRows=' . $maxgeonames;

my ($sqlite_db) = @ARGV;
die "Parameter is sqlite database" if !$sqlite_db;
my $exists = -e $sqlite_db;
my $dbh = DBI->connect("dbi:SQLite:dbname=$sqlite_db", "", "", { AutoCommit => 0 });
if (!$exists) {
    $dbh->do(<<END);
    CREATE TABLE cache (
        url text not null,
        content text not null,
        primary key(url)
    );
END
    $dbh->commit();
}


print "geonameId\tfcode\tname\tpopulation\twikipedia\tlat\tlon\tduration\tactual_to\tswitched\n";

#my $lat = shift;
#my $lon = shift;
#for (my $s = $lat - 0.2; $s <= $lat + 0.2; $s += 0.1) {
#    for (my $w = $lon - 0.2; $w <= $lon + 0.2; $w += 0.1) {

for (my $s = 36.1; $s <= 70.7; $s += $spacing) {
    for (my $w = -9.7; $w <= 27.0; $w += $spacing) {
        my $n = $s + $spacing;
        my $e = $w + $spacing;
        print STDERR "W:$w E:$e S:$s N:$n...\n";
        my $urlF = sprintf($url, $n, $s, $e, $w);
        print STDERR "geonames: $urlF\n";

        my $cities = $dbh->selectrow_array('select content from cache where cache.url = ?', {}, $urlF);
        if (!$cities) {
            $cities = get($urlF);
            $dbh->do('insert into cache (url, content) values (?, ?)', {}, $urlF, $cities);
            $dbh->commit();
        }
        $cities = from_json($cities, { utf8 => 1 });
        if ($cities->{status}) {
            print STDERR $cities->{status}{message} . "\n";
            next;
        }

        $cities = $cities->{geonames};
        foreach (@$cities) {
            my $city = $_;
            my $name = $city->{name};
            print STDERR "bahn.de: $name\n";

            my $dur = $dbh->selectrow_array('select content from cache where cache.url = ?', {}, $name);
            if (!$dur) {
                $dur = get_timings($name);
                $dbh->do('insert into cache (url, content) values (?, ?)', {}, $name, $dur);
                $dbh->commit();
                sleep 1;
            }
            utf8::encode($name);
            print "$city->{geonameId}\t$city->{fcode}\t$name\t$city->{population}\t$city->{wikipedia}\t$city->{lat}\t$city->{lng}\t$dur\n";
        }
    }
}
