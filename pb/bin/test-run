#!/usr/bin/perl -w -I../../perllib
#
# test-run:
# Test harness for PledgeBank.  Makes sure we haven't broken the code.
# 
# Requires:
# * ../general/conf file set up for PledgeBank, and matching the below requirements
# * apache configured to serve ../web on OPTION_BASE_URL
# * a database with name ending "_testharness"; this script will drop and remake the
#   database, so make sure it is never used for anything important
# * email addresses (email_n below) configured to pipe to ./test-mailin with fast
#   local delivery
#
# Copyright (c) 2005 UK Citizens Online Democracy. All rights reserved.
# Email: francis@mysociety.org; WWW: http://www.mysociety.org/
#

# Tests to do:
# Try to sign up to an expired pledge
# Signing up to a pledge after midnight of failure should be disallowed
# Test exactly vs. at least works
# Check other private pages are private (e.g. flyers)
# Send SMS messages somehow without costing us a fortune

my $rcsid = ''; $rcsid .= '$Id: test-run,v 1.3 2005-03-11 18:33:20 francis Exp $';

use strict;
require 5.8.0;

use Data::Dumper;
use Carp;

use mySociety::Config;
mySociety::Config::set_file('../conf/general');
use mySociety::WebTestHarness;

our $base_url = mySociety::Config::get('BASE_URL');
our $httpd_error_log = mySociety::Config::get('HTTPD_ERROR_LOG');
sub email_n { my $n = shift; return "pbharness+$n\@owl"; }
sub name_n { my $n = shift; return $n == 0 ? "Peter Setter" : "Siegfried Signer $n"; }
my $verbose = 1;

#############################################################################
# Setup

# Configure test harness class
print "Set up web test harness...\n" if $verbose > 0;
our $wth = new mySociety::WebTestHarness('PB_');
$wth->log_watcher_setup($httpd_error_log);
$wth->database_drop_reload('../db/schema.sql');
$wth->email_setup();
our $b = $wth->browser_get_agent();

# Syntax check all .php files
print "Syntax check all PHP files...\n" if $verbose > 0;
$wth->php_check_syntax("../../pb/");

# Check that we can detect PHP errors
print "Confirm we can detect errors...\n" if $verbose > 0;
$b->get($base_url . "/test.php?error=1" );
#die "Unable to detect errors from PHP" if !$wth->log_watcher_get_errors();

#############################################################################
# Functions to make and sign pledges, and so on

# sign_pledge WHO SHOW_SIGNATURE
# Adds a signer to a pledge, assumes browser is already pointing to pledge
# signup page.  Adds person number WHO.  SHOW_SIGNATURE is 1 for their
# signature to be public, undef for private.
sub sign_pledge {
    my ($who, $show_signature) = @_;

    $wth->browser_check_contents("Sign me up");
    $b->submit_form(form_name => 'pledge',
        fields => { 
            name => name_n($who), email => email_n($who), showname => $show_signature 
            },
        button => 'submit') or die "Failed to submit signing form";
    $wth->browser_check_contents("We've sent you an email to confirm your address");
    $wth->log_watcher_check();
}

# find_and_sign_pledge ACTION WHO SHOW_SIGNATURE
# Finds a public pledge from the front page use text ACTION, then calls
# sign_pledge.
sub find_and_sign_pledge {
    my ($action, $who, $show_signature) = @_;

    $b->get($base_url);
    $b->follow_link(text_regex => qr/$action/) or die "Pledge not appeared on front page";
    sign_pledge($who, $show_signature);
}

# confirm_signature ACTION WHO TARGET_REACHED
# Looks for confirmation email to WHO with pledge action ACTION.  Follows link to
# confirm the signature.  TARGET_REACHED is true if this is expected to have
# made pledge reach target, or false otherwise.
sub confirm_signature {
    my ($action, $who, $target_reached) = @_;
    my $confirmation_email = $wth->email_get_containing(
        '%To: '.email_n($who).
        '%Subject: Signing up to "'.$action.'"'.
        '%To confirm%');
    print "Confirmation link not found\n" if ($confirmation_email !~ m#^\s*($base_url.*$)#m);
    $b->get($1);
    if ($target_reached) {
        $wth->browser_check_contents("Your signature has made this pledge reach its target!");
    } else {
        $wth->browser_check_contents("Thank you for confirming your signature");
    }
    $wth->log_watcher_check();
}

# create_pledge WHO PAGE1 PAGE2
# Creates a new pledge, setter is person number WHO.  PAGE1
# and PAGE2 contain parameters for each page of the sign-up form.
sub create_pledge {
    my ($who, $page1, $page2) = @_;
    $page1->{name} = name_n($who); 
    $page1->{email} = email_n($who);

    $b->get($base_url);
    $b->follow_link(text_regex => qr/Start your own pledge/) or die "Start your own pledge link missing";
    $wth->browser_check_contents("New Pledge");
    $b->submit_form(form_name => 'pledge', fields => $page1,
        button => 'submit') or die "Failed to submit creating form stage 1";
    $wth->browser_check_contents("Step 2");
    $wth->log_watcher_check();
    $b->submit_form(form_name => 'pledge',
        fields => $page2,
        button => 'submit') or die "Failed to submit creating form stage 2";
    $wth->browser_check_contents("You must now click on the link within the email we've just sent you");
    $wth->log_watcher_check();
    my $confirmation_email = $wth->email_get_containing(
        '%To: '.email_n($who).'%To confirm your email address%');
    die "Confirmation link not found\n" if ($confirmation_email !~ m#^($base_url.*$)#m);
    $b->get($1);
    $wth->browser_check_contents("Thank you for confirming your pledge");
}

#############################################################################
print "Making private pledge and letting it expire...\n" if $verbose > 0;

my $pledge_action_private = 'check private pledges are private';
my $private_password = 'zx3uuhai';
create_pledge(0, 
    { title => $pledge_action_private, 
        target => '10', date => '2010-06-01', ref => 'privatepledge' 
    },
    { visibility => 'password', password => $private_password }
);
$b->get($base_url);
$wth->browser_check_no_contents($pledge_action_private);
$b->get($base_url . "/privatepledge");
$wth->browser_check_contents("Password Protected Pledge");
$b->submit_form(form_name => 'pledge',
        fields => { pw => 'thisisnotthepassword' },
        button => 'submit') or die "Failed to submit password form";
$wth->browser_check_contents("Incorrect password!");
$b->submit_form(form_name => 'pledge',
        fields => { pw => $private_password },
        button => 'submit') or die "Failed to submit password form";
$wth->browser_check_contents($pledge_action_private);

sign_pledge(1, 1);
confirm_signature($pledge_action_private, 1);

# Test what happens upon expiry
system("php", "daily", "--verbose", "--today=2010-06-02") and die "Failed to call daily";
$wth->email_get_containing('%To: '.email_n(0).  '%did not meet its target in the required time%');
$wth->email_get_containing('%To: '.email_n(1).  '%did not meet its target in the required time%');

#############################################################################
print "Making public pledge and making it succeed...\n" if $verbose > 0;

my $pledge_action_completion = 'make sure a pledge can be completed';
create_pledge(0, 
    { title => $pledge_action_completion, 
        target => '3', type => 'automated lines of code', signup => 'do the same',
        date => 'tomorrow', ref => 'automatedtest',
    },
    {  }
);
# Sign it a few times
for (my $i = 1; $i <= 3; ++$i) {
    #print "Signing the pledge $i...\n" if $verbose > 0;
    # Don't show name for one of the users
    my $show_signature = ($i == 2) ? undef : 1;
    find_and_sign_pledge($pledge_action_completion, $i, $show_signature);
}
for (my $i = 1; $i <= 3; ++$i) {
    #print "Confirming signature $i...\n" if $verbose > 0;
    my $expect_target = ($i == 3);
    confirm_signature($pledge_action_completion, $i);
}
# To do, make extra signature and check doesn't work
#find_and_sign_pledge($pledge_action_completion, 4, 1);
#confirm_signature($pledge_action_completion, 4);
# Check it has completed
system("php", "daily", "--verbose") and die "Failed to call daily";
$b->get($base_url);
$b->follow_link(text_regex => qr/$pledge_action_completion/) or die "Pledge not appeared on front page";
$wth->browser_check_contents("This pledge has been successful!");
$wth->browser_check_contents("Plus 1 other who did not want to give their name");
for (my $i = 0; $i <= 3; ++$i) {
    # Check email there or not there
    if ($i == 2) {
        $wth->browser_check_no_contents(name_n($i));
    } else {
        $wth->browser_check_contents(name_n($i));
    }
    # Check got success emails
    my $confirmation_email = $wth->email_get_containing( '%To: '.email_n($i).'%Congratulations%');
}
$wth->log_watcher_check();

#############################################################################
# Check for any unhandled mails or errors

$wth->email_check_none_left();
$wth->log_watcher_check();

