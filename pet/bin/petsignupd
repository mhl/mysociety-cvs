#!/usr/bin/perl -w
#
# petsignupd:
# Back-end signup service for petitions site.
#
# Clients -- the web scripts on the front-end servers -- send signup details to
# us in UDP packets. On receipt of such a packet we record that a signature is
# to be made, but do not make any change to the database. Instead we set an
# alarm for a short interval in the future. Any more packets that arrive before
# the alarm fires are also recorded; when the alarm fires, we insert them all,
# commit the changes, and acknowledge them to the senders.
#
# The idea here is that a commit of ten inserts takes about as long as a commit
# of one insert, so we can improve throughput by batching inserts. That's very
# inconvenient in the front-end scripts, because it means they'd have to share
# database handles, but is easy with a coordinating daemon. If the daemon fails
# or the packets don't get through, then the web scripts emit an error.
#
# TODO: proper logging; make it a proper daemon.
#
# Copyright (c) 2006 UK Citizens Online Democracy. All rights reserved.
# Email: chris@mysociety.org; WWW: http://www.mysociety.org/
#

my $rcsid = ''; $rcsid .= '$Id: petsignupd,v 1.21 2006-09-04 16:26:27 chris Exp $';

use strict;

# Horrible boilerplate to set up appropriate library paths.
use FindBin;
use lib "$FindBin::Bin/../perllib";
use lib "$FindBin::Bin/../../perllib";

use mySociety::Config;
BEGIN {
    mySociety::Config::set_file("$FindBin::Bin/../conf/general");
}

use Carp;
use Errno;
use Error qw(:try);
use Getopt::Long;
use IO::Select;
use IO::Socket;
use Pod::Usage;
use Socket;
use Tie::Cache;
use Time::HiRes qw(time alarm);

use mySociety::DBHandle qw(dbh);

use Petitions;
use Petitions::RPC;

# do_pending
# Actually perform the queue of requests against the database.
my %pending = ( );
my %cache;
my $s;
sub do_pending () {
    my $t0 = time();
    
    foreach my $r (values(%pending)) {
        if (exists($r->{confirm})) {
            Petitions::RPC::confirm_db($r);
        } else {
            Petitions::RPC::sign_petition_db($r);
        }
    }
    dbh()->commit();

    my $t1 = time();
    printf_log('debug', 'writing to db took %fs', $t1 - $t0);

    $s->blocking(1);
    my $maxtime = 0;
    foreach my $r (values(%pending)) {
        $cache{$r->{cookie}} = 1;
        my $packet = Petitions::RPC::make_ack_packet($r->{cookie});
        my ($port, $ip) = sockaddr_in($r->{client});
        if (!$s->send($packet, 0, $r->{client})) {
            print_log('err', "sending ack to @{[ inet_ntoa($ip) ]}:$port: $!");
        } else {
            $maxtime = time() - $r->{time} if (time() - $r->{time} > $maxtime);
            printf_log('debug', 'send ack to %s:%d cookie %s',
                        inet_ntoa($ip), $port, unpack('h*', $r->{cookie}));
        }
    }
    $s->blocking(0);

    printf_log('debug', 'sending acks took %fs; max %fs since request received',
                time() - $t1, $maxtime);
}

# Entry point.
my $max_signup_time = mySociety::Config::get('MAX_SIGNUP_TIME', 0.1);
my $pidfilepath = mySociety::Config::get('PETSIGNUPD_PIDFILE', '/var/run/petsignupd.pid');
my $debug = 0;
my $help = 0;
if (!GetOptions(
        'pidfile=s' =>  \$pidfilepath,
        debug =>        \$debug,
        help =>         \$help
    )) {
    print_log('err', "bad options; try --help for help");
    exit(1);
}

if ($help) {
    pod2usage(-exitval => 0, -verbose => 1);
    exit(0);
}

mySociety::Util::log_to_stder(0) if (!$debug);
mySociety::Util::daemon() unless ($debug);

my $pidfile;
try {
    $pidfile = new mySociety::PIDFile($pidfilepath);
} catch mySociety::PIDFile::Error with {
    my $E = shift;
    print_log('err', $E->text());
    exit(1);
};

print_log('info', "starting up");

$s = new IO::Socket::INET(
                    LocalAddr => '0.0.0.0',
                    LocalPort => mySociety::Config::get('PETSIGNUPD_PORT'),
                    Type => SOCK_DGRAM,
                    Proto => 'udp', # Socket.pm defines IPPROTO_TCP but not
                                    # IPPROTO_UDP. Lame lame lame.
                    ReuseAddr => 1,
                    Blocking => 0) or die "socket: $!";

my $foad = 0;
$SIG{TERM} = $SIG{INT} = sub { $foad = 1; };

my $alarm_pending = 0;
my $alarm_fired = 0;
$SIG{ALRM} = sub { $alarm_fired = 1; $alarm_pending = 0; };

tie(%cache, 'Tie::Cache', 5000);

my $t0;
while (!$foad) {
    my $buf = '';
    my $sender;
    my @ready = IO::Select->new($s)->can_read($max_signup_time / 10);
    if (@ready) {
        while (defined($sender = $s->recv($buf, 65536, 0))) {
            if (my $r = Petitions::RPC::parse_packet($buf)) {
                # We have a valid request. Schedule an alarm so that we will
                # send a response within the deadline.
                if (!$alarm_pending) {
                    $t0 = time();
                    $alarm_pending = 1;
                    alarm($max_signup_time);
                }

                # Record address to which we send acknowledgement.
                $r->{client} = $sender;
                $r->{time} = time();

                # See if this is a packet we've recently processed. If it is,
                # immediately send an acknowledgement. Otherwise, add it to the
                # list of requests to process later.
                if (exists($cache{$r->{cookie}})) {
                    my $ack = Petitions::RPC::make_ack_packet($r->{cookie});
                    $s->send($ack, 0, $r->{client});
                        # don't care much if this fails
                } elsif (!exists($pending{$r->{cookie}})) {
                    $pending{$r->{cookie}} = $r;
                }

                # Done handling this request.
            } else {
                # Invalid packet of some sort. Log and continue.
                my ($port, $ip) = sockaddr_in($sender);
                printf_log('err', 'bad packet from %s:%d',
                            inet_ntoa($ip), $port);
            }
        }
        
        if (!$!{EAGAIN}) {
            print_log('err', 'recvfrom: %s', $!);
        }
    }

    # If the alarm has fired, then do the signatures and send the
    # acknowledgements to the clients.
    if ($alarm_fired && keys(%pending)) {
        my $n = scalar(keys(%pending));
        printf_log('debug', 'alarm fired; have %d signatures to make', $n);
        try {
            do_pending();
            $alarm_fired = 0;
        } catch mySociety::DBHandle::Error with {
            my $E = shift;
            printf_log('err', 'committing %d requests failed; DB error: %s',
                        scalar(keys(%pending)), $E->text());
        };
        %pending = ( );
        my $t1 = time();
        printf_log('debug', 'time from first request %fs; %f sigs/s',
                    $t1 - $t0, $n / ($t1 - $t0));
    }
}

if (keys(%pending)) {
    try {
        do_pending();
    } catch mySociety::DBHandle::Error with {
        my $E = shift;
        printf_log('err', 'committing %d requests failed (at shutdown); DB error: %s',
                    scalar(keys(%pending)), $E->text());
    };
}

untie(%cache);

exit(0);

__END__

=head1 NAME

petsignupd

=head1 SYNOPSIS

petsignupd --help | [OPTIONS]

=head1 DESCRIPTION

Signup-processing daemon for the petitions site.

=head1 OPTIONS

=over 4

=item --help

Display information about using the program.

=item --debug

Don't detach from controlling terminal; log diagnostics to standard error as
well as the system log.

=item --pidfile PATH

Use PATH as the location of the PID file. By default, uses the value defined in
the config file, or "/var/run/petsignupd.pid".

=back

=head1 COPYRIGHT

Copyright (c) 2006 UK Citizens Online Democracy

=head1

$Id: petsignupd,v 1.21 2006-09-04 16:26:27 chris Exp $

