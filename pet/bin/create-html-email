#!/usr/bin/perl -w -I../perllib -I../../perllib

# create-html-email:
# Used to create mail template for sending HTML email response
# 
# Create a file in the same directory, name the reference of the petition.
# The format of this file is:
#
# <Subject>\n\n<Initial paragraph>\n\n\n<Paragraphs of text, separated by \n\n>
# \n\n\n"Further information"\n\n
# <Paragraphs of text and links, with links specified like [URL <Text for link>]>
# 
# Automatically adds the optout line

use strict;
use Petitions::HTMLEmail;
use File::Slurp;
use Net::SMTP;

my $ref = shift;
my $test = shift || '';
unless ($ref) {
    print "Usage: $0 REF [EMAIL]\n";
    exit;
}

my $text = File::Slurp::read_file($ref);
$text =~ s/\r//g;
my ($subject) = $text =~ /^(.*)\n+/;
$text =~ s/^$subject\n+//;
$text =~ s/\n+$//;
$text .= "\n\nIf you would like to opt out of receiving further mail on this or any other petitions you signed, please email [optout\@petitions.pm.gov.uk]";

my $email = Petitions::HTMLEmail::construct_email($text, $subject);

# And send it!
if ($test) {
    my $smtp = Net::SMTP->new('localhost');
    $smtp->mail($test);
    $smtp->to($test) or die "Could not set TO: $!";
    $smtp->data($email) or die "Could not send DATA: $!";
    $smtp->quit();
} else {
    print $email;
}
